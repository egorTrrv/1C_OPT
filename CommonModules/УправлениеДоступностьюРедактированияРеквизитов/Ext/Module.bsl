Процедура ЗаблокироватьРеквизиты(Форма, Ссылка) Экспорт
	
	ТипыСБлокировокой = ТипыОбъектовСИспользованиемБлокировки();
	Если ТипыСБлокировокой.Найти(ТипЗнч(Ссылка)) = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	СписокРеквизитов = СписокРеквизитов(Ссылка);
	Для Каждого Реквизит Из СписокРеквизитов Цикл
		
		Форма.Элементы[Реквизит].ТолькоПросмотр = Истина;
		
	КонецЦикла;
	
	Если УправлениеДоступомСервер.ПроверитьРоль("РедактированиеЗаблокированныхРеквизитов")
		И Форма.Команды.Найти("РазрешитьРедактированиеРеквизитов") = Неопределено Тогда
		
		//Добавление команды формы
		Команда = Форма.Команды.Добавить("РазрешитьРедактированиеРеквизитов"); //Имя команды
		Команда.Заголовок = "Разрешить редактирование реквизитов";
		Команда.Действие  = "Подключаемый_РазрешитьРедактированиеРеквизитов"; //Имя связанной процедуры
		
		//Добавление кнопки формы
		КнопкаФормы = Форма.Элементы.Добавить(
			"КнопкаРазрешитьРедактированиеРеквизитов",
			Тип("КнопкаФормы"),
			Форма.КоманднаяПанель);
			
		КнопкаФормы.ИмяКоманды = "РазрешитьРедактированиеРеквизитов"; //Связь с командой по имени
		КнопкаФормы.Вид = ВидКнопкиФормы.КнопкаКоманднойПанели;
		КнопкаФормы.Картинка = БиблиотекаКартинок.Изменить;
		КнопкаФормы.Отображение = ОтображениеКнопки.КартинкаИТекст;
		КнопкаФормы.ПоложениеВКоманднойПанели = ПоложениеКнопкиВКоманднойПанели.ВДополнительномПодменю;
		
	КонецЕсли;
	
	Если Форма.Команды.Найти("РазрешитьРедактированиеРеквизитов") <> Неопределено Тогда
		Форма.Элементы["КнопкаРазрешитьРедактированиеРеквизитов"].Видимость = Истина;
	КонецЕсли;
	
	Если Форма.Команды.Найти("ЗавершитьРедактированиеРеквизитов") <> Неопределено Тогда
		Форма.Элементы["КнопкаЗавершитьРедактированиеРеквизитов"].Видимость = Ложь;
	КонецЕсли;
	
КонецПроцедуры

Процедура РазблокироватьРеквизиты(Форма, Ссылка) Экспорт
	
	ТипыСБлокировокой = ТипыОбъектовСИспользованиемБлокировки();
	Если ТипыСБлокировокой.Найти(ТипЗнч(Ссылка)) = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	СписокРеквизитов = СписокРеквизитов(Ссылка);
	Для Каждого Реквизит Из СписокРеквизитов Цикл
		
		Форма.Элементы[Реквизит].ТолькоПросмотр = Ложь;
		
	КонецЦикла;
	
	Если УправлениеДоступомСервер.ПроверитьРоль("РедактированиеЗаблокированныхРеквизитов")
		И Форма.Команды.Найти("ЗавершитьРедактированиеРеквизитов") = Неопределено Тогда
		
		Форма.Элементы["КнопкаРазрешитьРедактированиеРеквизитов"].Видимость = Ложь;
		
		//Добавление команды формы
		Команда = Форма.Команды.Добавить("ЗавершитьРедактированиеРеквизитов"); //Имя команды
		Команда.Заголовок = "Завершить редактирование реквизитов";
		Команда.Действие  = "Подключаемый_ЗавершитьРедактированиеРеквизитов"; //Имя связанной процедуры
		
		//Добавление кнопки формы
		КнопкаФормы = Форма.Элементы.Добавить(
			"КнопкаЗавершитьРедактированиеРеквизитов",
			Тип("КнопкаФормы"),
			Форма.КоманднаяПанель);
			
		КнопкаФормы.ИмяКоманды = "ЗавершитьРедактированиеРеквизитов"; //Связь с командой по имени
		КнопкаФормы.Вид = ВидКнопкиФормы.КнопкаКоманднойПанели;
		КнопкаФормы.Картинка = БиблиотекаКартинок.ОформлениеФлажок;
		КнопкаФормы.Отображение = ОтображениеКнопки.КартинкаИТекст;
		КнопкаФормы.ПоложениеВКоманднойПанели = ПоложениеКнопкиВКоманднойПанели.ВКоманднойПанели;
		
	КонецЕсли;
	
КонецПроцедуры

Функция ТипыОбъектовСИспользованиемБлокировки()

	Типы = Новый Массив;
	Типы.Добавить(Тип("ДокументСсылка.ЗаказНаЗакупку"));
	Типы.Добавить(Тип("ДокументСсылка.ЗаказНаПродажу"));
	Типы.Добавить(Тип("ДокументСсылка.ДвижениеТовара"));

	Возврат Типы;

КонецФункции // ТипыОбъектовСИспользованиемБлокировки()

Функция СписокРеквизитов(Ссылка)

	СписокРеквизитов = Документы[Ссылка.Метаданные().Имя].РеквизитыНедоступныеДляРедактирования();
	Возврат СписокРеквизитов;

КонецФункции // СписокРеквизитов()
