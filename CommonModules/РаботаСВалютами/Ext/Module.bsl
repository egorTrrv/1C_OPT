#Область ПрограммныйИнтерфей

Процедура ВыполнитьНачальнуюЗагрузкуВалют() Экспорт
	
	Если Константы.ВалютыЗагружены.Получить() Тогда
		ВызватьИсключение "Нельзя выполнить начальную загрузку: валюты уже загружены!";
		Возврат;
	КонецЕсли;
	
	Прокси = WSСсылки.CBR.СоздатьWSПрокси("http://web.cbr.ru/", "DailyInfo", "DailyInfoSoap");
    
    ТипWSПараметра = Прокси.ФабрикаXDTO.Пакеты.Получить("http://web.cbr.ru/").Получить("EnumValutes");
    WSПараметр  = Прокси.ФабрикаXDTO.Создать(ТипWSПараметра);
    WSПараметр.Seld = Ложь;
    
    Валюты = Прокси.EnumValutes(WSПараметр);
    
	Для Каждого Элемент Из Валюты.EnumValutesResult.diffgram.ValuteData.EnumValutes Цикл
		
		Попытка
			НовыйЭлемент = Справочники.Валюты.СоздатьЭлемент();
			НовыйЭлемент.Наименование = Элемент.Vname;
			НовыйЭлемент.СимвольныйКод = Элемент.VcharCode;
			НовыйЭлемент.ЦифровойКод = Элемент.VcommonCode;
			НовыйЭлемент.Записать();
		Исключение
			ЗаписьЖурналаРегистрации("Ошибка записи валюты", УровеньЖурналаРегистрации.Ошибка,,,ОписаниеОшибки(),);
			Продолжить;
		КонецПопытки;
		
	КонецЦикла;
	
	Константы.ВалютыЗагружены.Установить(Истина);

КонецПроцедуры

Процедура ВыполнитьЕжедневноеЗаполнениеКурсовВалют() Экспорт
	
	ДатаПоследнейЗагрузки = Константы.ДатаПоследнейЗагрузкиКурсовВалют.Получить();
	Если ДатаПоследнейЗагрузки = День(ТекущаяДата()) Тогда
		Сообщить("Валюты уже загружены!");
		Возврат;
	КонецЕсли;
	
	ДатаЗагрузки = ТекущаяДата();
	КурсыВалютXDTO = КурсыВалют(ДатаЗагрузки);
	
	Если КурсыВалютXDTO = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ТЗ_КурсыВалют = Новый ТаблицаЗначений;

	ОписаниеТипаЧисло = Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(10,5));
	ОписаниеТипаСтрока = Новый ОписаниеТипов("Строка",,Новый КвалификаторыСтроки(100));
	
	ТЗ_КурсыВалют.Колонки.Добавить("Vname", ОписаниеТипаСтрока);
	ТЗ_КурсыВалют.Колонки.Добавить("Vnom", ОписаниеТипаЧисло);
	ТЗ_КурсыВалют.Колонки.Добавить("VChCode", ОписаниеТипаСтрока);
	ТЗ_КурсыВалют.Колонки.Добавить("Vcurs", ОписаниеТипаЧисло);
	
	Для Каждого КурсXDTO Из КурсыВалютXDTO Цикл
		
		НоваяСтрокаТЗ = ТЗ_КурсыВалют.Добавить();
		НоваяСтрокаТЗ.Vname		= КурсXDTO.Vname;
        НоваяСтрокаТЗ.Vnom      = КурсXDTO.Vnom;
        НоваяСтрокаТЗ.VChCode 	= КурсXDTO.VChCode;
        НоваяСтрокаТЗ.Vcurs     = КурсXDTO.Vcurs;
		
	КонецЦикла;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	Валюты.СимвольныйКод КАК СимвольныйКод,
		|	Валюты.Ссылка КАК Ссылка
		|ПОМЕСТИТЬ втВалютыДляЗаписи
		|ИЗ
		|	Справочник.Валюты КАК Валюты
		|ГДЕ
		|	Валюты.ЗагружатьЕжедневно = ИСТИНА
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	КурсыВалют.Vname КАК Название,
		|	КурсыВалют.Vnom КАК Номинал,
		|	КурсыВалют.VChCode КАК СимвольныйКод,
		|	КурсыВалют.Vcurs КАК Курс
		|ПОМЕСТИТЬ втКурсыВалют
		|ИЗ
		|	&ТаблицаКурсовВалют КАК КурсыВалют
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	втВалютыДляЗаписи.Ссылка КАК Ссылка,
		|	втВалютыДляЗаписи.СимвольныйКод КАК СимвольныйКод,
		|	втКурсыВалют.Название КАК Название,
		|	втКурсыВалют.Номинал КАК Номинал,
		|	втКурсыВалют.Курс КАК Курс
		|ИЗ
		|	втВалютыДляЗаписи КАК втВалютыДляЗаписи
		|		ЛЕВОЕ СОЕДИНЕНИЕ втКурсыВалют КАК втКурсыВалют
		|		ПО втВалютыДляЗаписи.СимвольныйКод = втКурсыВалют.СимвольныйКод";
	
	Запрос.УстановитьПараметр("ТаблицаКурсовВалют", ТЗ_КурсыВалют);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Результат = РезультатЗапроса.Выгрузить();
	
	НаборЗаписей = РегистрыСведений.КурсыВалют.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.Период.Установить(ДатаЗагрузки);
	
	Для Каждого Строка Из Результат Цикл
		
		Запись = НаборЗаписей.Добавить();
		Запись.Период = ДатаЗагрузки;
		Запись.Валюта = Строка.Ссылка;
		Запись.СимвольныйКод = Строка.СимвольныйКод;
		Запись.НазваниеВалюты = Строка.Название; 
		Запись.Номинал = Строка.Номинал;
		Запись.Курс = Строка.Курс;
		
	КонецЦикла;
	
	НаборЗаписей.Записать();
	Константы.ДатаПоследнейЗагрузкиКурсовВалют.Установить(ДатаЗагрузки);

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция КурсыВалют(Дата)
	
	Попытка
		
		Прокси = WSСсылки.CBR.СоздатьWSПрокси("http://web.cbr.ru/", "DailyInfo", "DailyInfoSoap");
	    ТипWSПараметра = Прокси.ФабрикаXDTO.Пакеты.Получить( "http://web.cbr.ru/").Получить("GetCursOnDate");
	    WSПараметр = Прокси.ФабрикаXDTO.Создать(ТипWSПараметра);
	    WSПараметр.On_Date = Дата;
	    
	    КурсыВалют = Прокси.GetCursOnDate(WSПараметр);
	   	Возврат КурсыВалют.GetCursOnDateResult.diffgram.ValuteData.ValuteCursOnDate; 
		
	Исключение
		
		Сообщить(КраткоеПредставлениеОшибки(ИнформацияОбОшибке()));
		Возврат Неопределено;
		
	КонецПопытки;
КонецФункции

#КонецОбласти
